-- ============================================
-- Location & Battle Map
-- ============================================
World := OBJECT{
    location_description: '',
    user_latitude: 56.28,
    user_longitude: 13.28,
    battle_locations: [],

    SET_LOCATION_DESCRIPTION: (value) => BEGIN
        location_description := value;
        PUBLISH('World.location_description');
    END,

    SET_USER_COORDINATES: (lat, lon) => BEGIN
        user_latitude := lat;
        user_longitude := lon;
        PUBLISH('World.user_coordinates');
    END,

    -- Set location description and optionally coordinates in one call.
    SET_LOCATION: (__desc, __lat, __lon) => BEGIN
        SET_LOCATION_DESCRIPTION(__desc);
        IF __lat <> null AND __lon <> null THEN
            SET_USER_COORDINATES(__lat, __lon);
    END,

    -- Generate the complete battle map widget tree (FlutterMap + markers).
    GENERATE_BATTLE_MAP: () => BEGIN
        __markers := [];
        __hero_list := MAP_VALUES(Heroes.heroes);
        IF LENGTH(__hero_list) > 0 THEN
        FOR i := 0 TO LENGTH(__hero_list) - 1 DO BEGIN
            hero := __hero_list[i];
            name := NVL(hero, h => h.name, "Unknown");
            alignment := BIOGRAPHY(hero, b => b.alignment, NEUTRAL);
            angle := i * 2.39996;
            radius := 0.05 + (i % 7) * 0.02;
            lat := user_latitude + radius * COS(angle);
            lon := user_longitude + radius * SIN(angle);
            __is_villain := alignment > GOOD;
            __color := IF __is_villain THEN '0xFFF44336' ELSE '0xFF2196F3';

            __marker_child := {"type": "Tooltip", "props": {
                "message": name,
                "child": {"type": "Container", "props": {
                    "decoration": {
                        "color": __color,
                        "shape": "circle",
                        "border": {"color": "0xFFFFFFFF", "width": 2},
                        "boxShadow": [{"blurRadius": 4, "color": "0x42000000"}]
                    },
                    "child": {"type": "Icon", "props": {"icon": IF __is_villain THEN "dangerous" ELSE "shield", "color": "0xFFFFFFFF", "size": 20}}
                }}
            }};

            __markers := __markers + [{"lat": lat, "lon": lon, "child": __marker_child}];
        END;

        RETURN {"type": "FlutterMap", "props": {
            "latitude": user_latitude,
            "longitude": user_longitude,
            "zoom": 10,
            "children": [
                {"type": "TileLayer", "props": {}},
                {"type": "MarkerLayer", "props": {"markers": __markers}}
            ]
        }};
    END,

    -- Weather
    weather_temp: null,
    weather_description: '',
    weather_icon: 'cloud',
    weather_wind: 0,

    SET_WEATHER: (temp, wind, description, icon) => BEGIN
        weather_temp := temp;
        weather_wind := wind;
        weather_description := description;
        weather_icon := icon;
        PUBLISH('World.weather');
    END,

    __WMO_DESCRIPTION: (code) => BEGIN
        IF code = 0 THEN RETURN 'Clear sky';
        IF code <= 3 THEN RETURN 'Partly cloudy';
        IF code <= 48 THEN RETURN 'Foggy';
        IF code <= 57 THEN RETURN 'Drizzle';
        IF code <= 67 THEN RETURN 'Rain';
        IF code <= 77 THEN RETURN 'Snow';
        IF code <= 82 THEN RETURN 'Rain showers';
        IF code <= 86 THEN RETURN 'Snow showers';
        IF code >= 95 THEN RETURN 'Thunderstorm';
        RETURN 'Cloudy';
    END,

    __WMO_ICON: (code) => BEGIN
        IF code = 0 THEN RETURN 'wb_sunny';
        IF code <= 3 THEN RETURN 'cloud';
        IF code <= 48 THEN RETURN 'foggy';
        IF code <= 67 THEN RETURN 'water_drop';
        IF code <= 77 THEN RETURN 'ac_unit';
        IF code <= 86 THEN RETURN 'ac_unit';
        IF code >= 95 THEN RETURN 'flash_on';
        RETURN 'cloud';
    END,

    REFRESH_WEATHER: () => BEGIN
        __url := 'https://api.open-meteo.com/v1/forecast?latitude=' + STRING(user_latitude) + '&longitude=' + STRING(user_longitude) + '&current_weather=true';
        __data := FETCH(__url);
        IF __data <> null AND __data["current_weather"] <> null THEN BEGIN
            __current := __data["current_weather"];
            __code := __current["weathercode"];
            SET_WEATHER(__current["temperature"], __current["windspeed"], __WMO_DESCRIPTION(__code), __WMO_ICON(__code));
        END;
    END,

    -- War Status
    war_messages: [
        "The invasion has been pushed back 47%",
        "Heroes are making progress in sector 7",
        "Villains have joined forces against the common enemy",
        "Defense grid holding steady",
        "New recruits arriving daily",
        "Morale is high among the defenders"
    ],

    GET_WAR_STATUS: () => BEGIN
        index := INT(Heroes.total_heroes) % LENGTH(war_messages);
        RETURN war_messages[index];
    END
};

-- Fetch weather on startup (must be after World is defined)
World.REFRESH_WEATHER();
